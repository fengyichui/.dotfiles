#
# GDB init local file
#

set $jlink_running = 0
set $runtype = 0

define hs662x
source ~/.dotfiles/.gdb/hs662x.gdb.py
end

define xx
source ~/.dotfiles/.gdb/dashboard.gdb
end

define invalidate_cache
set *0xE0042004 = 1
set *0xE0042008 = 0
set *0xE0042020 = 1
set *0xE0042004 = 0
set *0xE0042008 = 1
ignore-all mon exec InvalidateCache
end

define connect2local
ignore-all kill
target remote localhost:2331
end

define connect2remote
ignore-all kill
target remote 192.168.2.66:2331
end

define connect
#connect2remote
connect2local
disable_wdt
ignore-all mon exec EnableLowPowerHandlingMode
ignore-all mon exec SetAllowFlashCache = 0
end

define jlink
shell gdbserver-jlink -X --retry --speed=10000 -N="0xE00" -N="0x400"
set $jlink_running = 1
end

define jlink-stop
shell gdbserver-jlink --stop
set $jlink_running = 0
end

define hook-quit
if ($jlink_running)
ignore-all kill
ignore-all jlink-stop
end
end

define reconnect
ignore-all kill
jlink
connect
set $runtype = 0
end

define reconnect_hs6621
reconnect
mon device hs6621
set $runtype = 2
end

define reconnect_hs6621c
reconnect
mon device hs6621c
# CPU div=2
set *0x40001004 = 0x00c00206
set *0x4000100C = 1
set $runtype = 2
end

define reconnect_hs6621d_otp
reconnect
mon device hs6621d-otp
set $runtype = 3
end

# In Windows, use UsbDriverTool.exe to install 'libusb-WinUSB' with 'J-link'
define reconnect_openocd
ignore-all kill
target remote | openocd -c 'adapter driver jlink; transport select swd; set CHIPNAME CM; source [find target/fm4.cfg]; adapter speed 5000; gdb_port pipe; log_output /tmp/openocd.log;'
disable_wdt
end

define rc_rom
reloadfile
# 1st-rst: halt cpu
mon reset 1
load
# 2nd-rst: make MSP valid
mon reset 1
c
end

define rc_ram
reloadfile
remap ram
# 1st-rst: halt cpu
mon reset 1
load
# 2nd-rst: make MSP valid
mon reset 1
c
end

define rc_flash
reloadfile
# 1st-rst: halt cpu
mon reset 1
# Invalidate cache
invalidate_cache
# load
load
# set new sp and pc
set $sp = *0x50003000
set $pc = *0x50003004
c
end

define rc_otp
reloadfile
remap otp
# 1st-rst: halt cpu
mon reset 1
# Invalidate cache
invalidate_cache
# load
load
# 2nd-rst: make MSP valid
mon reset 1
c
end

define rc
if ($runtype == 0)
rc_rom
end
if ($runtype == 1)
rc_ram
end
if ($runtype == 2)
rc_flash
end
if ($runtype == 3)
rc_otp
end
end

define rrc
if $argc >= 1
file
shell makekeil rom/hs6620_rom.uvproj
end
mon reset 1
file rom/out/hs6620_rom.axf
restore rom/out/hs6620_rom.bin binary 0x08000000
mon reset 1
c
end
document rrc
Syntax: rcc [1]
| HS662x rom reset-load-continue
| 1: do makekeil
end

# vim: ft=gdb
